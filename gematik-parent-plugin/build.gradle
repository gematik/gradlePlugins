apply plugin: 'java'
if (!project.buildscript.configurations.getByName("classpath").filter { a -> a.toString().contains("nu.studer") }.isEmpty()) {
    apply plugin: 'nu.studer.credentials'
}
apply plugin: 'maven-publish'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'org.owasp.dependencycheck'

group = 'de.gematik'
version = file('version.txt').text.trim()

dependencies {
    compileOnly gradleApi()
}

if (project.hasProperty('signing.secretKeyRingFile')) {
    apply plugin: 'signing'

    signing {
        sign configurations.archives
    }
    model {
        tasks.generatePomFileForMavenJavaPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        tasks.publishMavenJavaPublicationToMavenLocal {
            dependsOn project.tasks.signArchives
        }

    }
}

javadoc {
    source = sourceSets.main.allJava
    failOnError = false
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    dependsOn("javadoc")
    from "${project.buildDir}/docs/javadoc/"
    into "${project.name}"
}

task groovydocJar(type: Jar) {
    archiveClassifier.set("groovydoc")
    dependsOn("groovydoc")
    from "${project.buildDir}/docs/groovydoc/"
    into "${project.name}"
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
    archives groovydocJar
}


publishing {
    repositories {
        maven {
            name = 'Nexus'
            credentials {
                username project.getProperty("SERVER_NEXUS_USERNAME")
                password project.getProperty("SERVER_NEXUS_PASSWORD")
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url project.getProperty("SNAPSHOT_REPOSITORY")
            } else {
                url project.getProperty("RELEASE_REPOSITORY")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifact sourceJar
            artifact javadocJar
            artifact groovydocJar
            from components.java
            if (project.hasProperty('signing.secretKeyRingFile')) {
                customizePom(pom)

                project.signing {
                    sign publishing.publications.mavenJava
                }
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'This Plugin adds functionality for Version handling with version.txt files'
            name 'Gematik Gradle Plugins'
            url 'https://github.com/gematik/gradlePlugins'
            organization {
                name 'de.gematik'
                url 'https://github.com/gematik'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/gematik/gradlePlugins/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                url 'https://github.com/gematik/' + 'gradlePlugins'
                connection 'scm:git:git://github.com/gematik/gradlePlugins.git'
                developerConnection 'scm:git:ssh://git@github.com:gematik/gradlePlugins.git'
            }
            developers {
                developer {
                    name 'gematik'
                    email 'software-development@gematik.de'
                    url 'https://gematik.github.io/'
                    organization 'gematik GmbH'
                    organizationUrl 'https://www.gematik.de/'
                }
            }
        }
    }
}

task deploy {
    println ""
    println ""
    println "##################################################################################"
    println "##################################################################################"
    println ""
    println ""
    println "           ######   ######## ##     ##    ###    ######## #### ##    ## "
    println "          ##    ##  ##       ###   ###   ## ##      ##     ##  ##   ##  "
    println "          ##        ##       #### ####  ##   ##     ##     ##  ##  ##   "
    println "          ##   #### ######   ## ### ## ##     ##    ##     ##  #####    "
    println "          ##    ##  ##       ##     ## #########    ##     ##  ##  ##   "
    println "          ##    ##  ##       ##     ## ##     ##    ##     ##  ##   ##  "
    println "           ######   ######## ##     ## ##     ##    ##    #### ##    ## "
    println ""
    println ""
    println "##################################################################################"
    println "##################################################################################"
    println ""
    println ""
    println "**********************************************************************************"
    println "**********************************************************************************"
    println "***********           Build Gematik Gradle Parent Plugin          ****************"
    println "***********                                                       ****************"
    println "**********************************************************************************"
    println "**********************************************************************************"
    println ""
    println ""
}

build.dependsOn javadoc
build.dependsOn groovydoc
publish.dependsOn build
publishToMavenLocal.dependsOn build
deploy.dependsOn publish
deploy.dependsOn publishToMavenLocal
